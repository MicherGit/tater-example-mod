plugins {
    id 'textile-loom' version '0.5.1+build12'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "Jitpack"
        url "https://jitpack.io/"
    }
    maven {
        name = "Concerning"
        url "https://maven.concern.i.ng/"
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    minecraft name: "${project.minecraft_version}", version: "${project.minecraft_version}"

    mappings files("libs/null.jar")

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Some dependencies aren't gotten by the loader, so we add them ourselves
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
    implementation 'com.google.guava:guava:28.0-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'org.ow2.asm:asm:9.0'
    implementation 'org.ow2.asm:asm-analysis:9.0'
    implementation 'org.ow2.asm:asm-commons:9.0'
    implementation 'org.ow2.asm:asm-tree:9.0'
    implementation 'org.ow2.asm:asm-util:9.0'
    annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    annotationProcessor group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.lwjgl.lwjgl:lwjgl_util:2.9.1-nightly-20130708-debug3') with module('org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209') 
            substitute module('org.lwjgl.lwjgl:lwjgl:2.9.1-nightly-20130708-debug3') with module('org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209')
        }
        force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
    }
}

minecraft {
    minecraft.intermediaryUrl = {
        return 'https://gist.githubusercontent.com/PheonixVX/a4dd5871c299d8c0e201965e29946f76/raw/a94b1987e17c54f9eb93f78e15397a8dd96f8964/inf-20100227.tiny'
    }
    customManifest = 'https://gist.githubusercontent.com/Chocohead/f522139742d22a65e33ef5481efb1740/raw/b17b970bb25fca4c4cee6ecf0cd149cae38a5891/inf-20100227.json'
    clientOnly = true
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}
